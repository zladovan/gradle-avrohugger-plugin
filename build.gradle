plugins {
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

repositories {
    mavenCentral()
}

group = 'com.zlad.gradle'

version = resolveVersion()

dependencies {
    implementation "com.julianpeeters:avrohugger-core_$scalaMajorVersion:$avrohuggerVersion"
    implementation "com.julianpeeters:avrohugger-filesorter_$scalaMajorVersion:$avrohuggerVersion"

    testImplementation("org.spockframework:spock-core:$spockVersion") {
        exclude group: 'org.codehaus.groovy'
    }
}

gradlePlugin {
    plugins {
        avrohuggerPlugin {
            id = 'com.zlad.gradle.avrohugger'
            implementationClass = 'com.zlad.gradle.avrohugger.AvrohuggerPlugin'
            displayName = 'Gradle avrohugger plugin'
            description = 'Plugin for generating scala case classes from apache avro schemas, datafiles and protocols.'
        }
    }
}

pluginBundle {
    website = 'https://github.com/zladovan/gradle-avrohugger-plugin'
    vcsUrl = 'https://github.com/zladovan/gradle-avrohugger-plugin.git'
    tags = ['scala', 'avro']
}

def resolveVersion() {
    def details = versionDetails()
    "${details.lastTag.replaceAll('^v', '')}.${details.commitDistance}" as String
}