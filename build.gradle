plugins {
    id 'groovy'
    id("com.gradle.plugin-publish") version "1.2.1"
    id 'com.palantir.git-version' version '3.0.0'
}

repositories {
    mavenCentral()
}

group = 'com.zlad.gradle'

version = resolveVersion()

dependencies {
    implementation "com.julianpeeters:avrohugger-core_$scalaBinaryVersion:$avrohuggerVersion"
    implementation "com.julianpeeters:avrohugger-filesorter_$scalaBinaryVersion:$avrohuggerVersion"

    testImplementation("org.spockframework:spock-junit4:$spockVersion")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    website = 'https://github.com/zladovan/gradle-avrohugger-plugin'
    vcsUrl = 'https://github.com/zladovan/gradle-avrohugger-plugin.git'
    plugins {
        create("avrohuggerPlugin") {
            id = 'com.zlad.gradle.avrohugger'
            implementationClass = 'com.zlad.gradle.avrohugger.AvrohuggerPlugin'
            displayName = 'Gradle avrohugger plugin'
            description = 'Plugin for generating scala case classes from apache avro schemas, datafiles and protocols.'
            tags.set(['scala', 'avro'])
        }
    }
}

def resolveVersion() {
    def details = versionDetails()
    "${details.lastTag.replaceAll('^v', '')}" as String
}